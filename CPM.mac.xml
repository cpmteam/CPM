<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Routine name="CPM" type="MAC" languagemode="0"><![CDATA[
#include CPM.License
#include %occInclude
#include CPM.LIB

SHELL
    new
    #; here goes interactive entry
    set pattern=##class(%Regex.Matcher).%New("\b[^ ]+\b|\""([^\""]+)\""")
    #; aliases for commands
    for i=1:1 {
        set line=$text(ALIASES+i)
        quit:$extract(line)'=" "
        set command=$zstrip($piece(line,";",2),"<>WCP")
        for j=3:1:$length(line,";") {
            set alias=$zconvert($piece(line,";",j),"U")
            continue:alias=""
            continue:$data(aliases(alias))
            set aliases(alias)=command
        }
    }
    for {
        write !,"CPM>> "
        read line
        set line=$zstrip(line,"<>WC")
        
        set command=$piece(line," ")
        set line=$extract(line,$length(command)+1,*)
        kill args
        set args=0
        set pattern.Text=line
        while pattern.Locate() {
            set argument=$zstrip(pattern.Group,"<>WC")
            set args($increment(args))=$zstrip(argument,"<>","""")
        }
        
        continue:command=""
        set command=$zconvert(command,"U")
        if $data(aliases(command),command)
        
        quit:command="QUIT"
        
        do BATCH(command, args...)
    }
    quit
BATCH(command, args...)
    ; here goes batch entry
    if $text(@command)="" {
        write !,"Unrecognized command: ",command,!
        quit
    }
    try {
        set command=command_"(args...)"
        do @command
    } catch {
        write !,"Error occured: ",$zerror
    }
    quit
ALIASES
 ;QUIT;Q
 ;INSTALL;I
 ;HELP;?
 ;LIST;L
COMMANDS
INSTALL(packages...) ;;install package
    do RegisterAllSourceTypes()
    set cpm=##class(CPM.Main).%New()
    do cpm.Install(packages...)
    quit
    new (packages)
    if '$data(packages) {
        write !,"Nothing to install"
        quit
    }
    do RegisterAllSourceTypes()
    write !,"Installing packages: "
    for i=1:1:packages {
        #dim package As %String = packages(i)
        #dim packageFile As CPM.SourceType.IBase = ##class(CPM.SourceType.IBase).%OpenContainer(package)
        if '$isobject(packageFile) {
            continue
        }
        if packageFile.IsPacked {
            do packageFile.Unpack()
        }
        #dim packageData As CPM.IPackage = packageFile.Parse()
        write !?3, packageData.Name, ?25, packageData.Version, ?45, packageData.Description
    }
    quit
UnpackPackage(pFileName) {
    set tFileStream=##class(%Stream.FileBinaryGzip).%New()
    set tFileStream.Filename=pFileName
    set result=##class(CPM.Utils.FileBinaryTar).ExtractStreamToArray(tFileStream)
    kill tFileStream
    quit result
}
SEARCH(terms...) ;;search packages
    set cpm=##class(CPM.Main).%New()
    do cpm.Search(terms...)
    quit
    
RegisterAllSourceTypes() {
    do ##class(CPM.SourceType.IBase).UnregisterExts()
    do ##class(CPM.SourceType.IBase).ForEachSuccessor("RegisterExts")
}

LIST(args...) ;;list of installed packages
    new (args)
    set sql=##class(%SQL.Statement).%New()
    set tSC=sql.%Prepare("SELECT Name, Version, Description FROM CPM.Package")
    quit:'$$$ISOK(tSC)
    set tRS=sql.%Execute()
    while tRS.%Next() {
        write !?3,tRS.Name,?15,tRS.Version,?25,tRS.Description
    }
    quit
HELP(command="",args...) ;;this help
    new (command,args)
    if command'="" {
        set cpm=##class(CPM.Main).%New()
        do cpm.Help(command,args...)
        quit 
    }
    write !,"Cache Package Manager"
    write !!,"Available commands:"
    for i=1:1 {
        set line=$text(COMMANDS+i)
        quit:line=""

        continue:$zstrip($extract(line),"*WCP")=""
        set help=$piece(line,";;",2)
        continue:help=""
        set command=$zconvert($piece(line,"("),"L")
        write !?3,command,?15,help
    }
    write !?3,"quit",?15,"quit from shell"
    write !
    quit
    

]]></Routine>
</Export>
