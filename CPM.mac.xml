<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Routine name="CPM" type="MAC" languagemode="0"><![CDATA[
#include CPM.License
#include %occInclude

SHELL
    new
    #; here goes interactive entry
    set pattern=##class(%Regex.Matcher).%New("\b[^ ]+\b|\""([^\""]+)\""")
    #; aliases for commands
    f i=1:1 {
        set line=$text(ALIASES+i)
        quit:$e(line)'=" "
        set command=$zstrip($piece(line,";",2),"<>WCP")
        f j=3:1:$l(line,";") {
            set alias=$zcvt($piece(line,";",j),"U")
            continue:alias=""
            continue:$data(aliases(alias))
            set aliases(alias)=command
        }
    }
    for {
        write !,"CPM>> "
        read line
        set line=$zstrip(line,"<>WC")
        
        set command=$piece(line," ")
        set line=$extract(line,$find(line," ")+1,*)
        kill args
        set args=0
        set pattern.Text=line
        while pattern.Locate() {
            set argument=$zstrip(pattern.Group,"<>WC")
            set args($increment(args))=$zstrip(argument,"<>","""")
        }
        
        continue:command=""
        set command=$zconvert(command,"U")
        if $d(aliases(command),command)
        
        quit:command="QUIT"
        
        d BATCH(command, args...)
    }
    quit
BATCH(command, args...)
    ; here goes batch entry
    if $text(@command)="" {
        write !,"Unrecognized command: ",command,!
        quit
    }
    try {
        set command=command_"(args...)"
        do @command
    } catch {
        write !,"Error occured: ",$zerror
    }
    quit
ALIASES
 ;QUIT;Q
 ;INSTALL;I
 ;HELP;?
 ;LIST;L
COMMANDS
INSTALL(packages...) ;;install package
	new (packages)
	if '$d(packages) {
		write !,"Nothing to install"
		quit
	}
	
	write !,"Installing packages: "
	for i=1:1:packages {
		set package=packages(i)
		set packageContent=""
		if ##class(%File).Exists(package) {
			set packageContent=$$UnpackPackage(package)
		}
		continue:'$isobject(packageContent)
		set packageContent=packageContent.GetNext()
		set metaInfo=packageContent.GetAt("package.json")
		continue:'$isobject(metaInfo)
		#dim metaInfoData As %Stream.Object = metaInfo.fileData
		continue:'##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(metaInfoData.Read(metaInfoData.Size),,.metaInfo)
		set name=metaInfo.name
		set description=$zcvt(metaInfo.description,"I","UTF8")
		set version=metaInfo.version
		write !?3,name,?25,version,?45,description
	}
	quit
UnpackPackage(pFileName) {
	set tFileStream=##class(%Stream.FileBinaryGzip).%New()
	set tFileStream.Filename=pFileName
	set result=##class(CPM.Utils.FileBinaryTar).ExtractStreamToArray(tFileStream)
	kill tFileStream
	quit result
}
CheckDependecies() {
}
LIST(args...) ;;list of installed packages
    new (args)
    set sql=##class(%SQL.Statement).%New()
    set tSC=sql.%Prepare("SELECT Name, Version, Description FROM CPM.Package")
    quit:'$$$ISOK(tSC)
    set tRS=sql.%Execute()
    while tRS.%Next() {
        w !?3,tRS.Name,?15,tRS.Version,?25,tRS.Description
    }
    quit
HELP(args...) ;;this help
    new (args)
    write !,"Cache Package Manager"
    write !!,"Available commands:"
    for i=1:1 {
        set line=$text(COMMANDS+i)
        quit:line=""

        continue:$zstrip($extract(line),"*WCP")=""
        set help=$piece(line,";;",2)
        continue:help=""
        set command=$zcvt($piece(line,"("),"L")
        write !?3,command,?15,help
    }
    w !?3,"quit",?15,"quit from shell"
    w !
    quit
    

]]></Routine>
</Export>
