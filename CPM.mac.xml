<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Routine name="CPM" type="MAC" languagemode="0"><![CDATA[
#include CPM.License
#include %occInclude
#include CPM.LIB

SHELL
    new
    #; here goes interactive entry
    #; aliases for commands
    do ALIASES(.aliases)
    for i=1:1 {
        set line=$text(ALIASES+i)
        quit:$extract(line)'=" "
        set command=$zstrip($piece(line,";",2),"<>WCP")
        for j=3:1:$length(line,";") {
            set alias=$zconvert($piece(line,";",j),"U")
            continue:alias=""
            continue:$data(aliases(alias))
            set aliases(alias)=command
        }
    }
    for {
        write !,"CPM:"_$Namespace_">> "
        read line
        do ExtractArgs(.line, .command, .args)
        
        continue:command=""
        set command=$zconvert(command,"U")
        if $data(aliases(command),command)
        
        quit:command="QUIT"
        
        do BATCH(command, args...)
    }
    quit

ExtractArgs(&line,&command,&args) {
    #dim pattern As %String = ##class(%Regex.Matcher).%New("[\b/-][^ ]+\b|\""([^\""]+)\""")

    set line    = $zstrip(line,"<>WC")
    set command = $piece(line," ")
    set line    = $piece(line," ",2,*)

    kill args
    set args = 0
    set pattern.Text = line
    while pattern.Locate() {
#if 0
        set argument = $zstrip(pattern.Group,"<>WC")
#else
        set argument = pattern.Group
#endif
        set args($increment(args)) = $zstrip(argument, "<>", """")
    }
}

BATCH(command="help", args...)
    ; here goes batch entry
    do ALIASES(.aliases)
    set command=$zconvert(command,"U")
    if $data(aliases(command),command)
    
    if $text(@command)="" {
        write !,"Unrecognized command: ",command,!
        quit
    }
    try {
        set command=command_"(args...)"
        do @command
    } catch {
        write !,"Error occured: ",$zerror
    }
    quit

ALIASES(aliases)
 ;QUIT;Q
 ;INSTALL;I
 ;HELP;?
 ;LIST;L
#; aliases for commands
    new (aliases)
    for i=1:1 {
        set line=$text(ALIASES+i)
        quit:$extract(line)'=" "
        set command=$zstrip($piece(line,";",2),"<>WCP")
        for j=3:1:$length(line,";") {
            set alias=$zconvert($piece(line,";",j),"U")
            continue:alias=""
            continue:$data(aliases(alias))
            set aliases(alias)=command
        }
    }
    quit
COMMANDS

RegisterAllSourceTypes() {
    do ##class(CPM.Source.IBase).UnregisterExts()
    do ##class(CPM.Source.IBase).ForEachSuccessor("RegisterExts")
}
INSTALL(packages...) ;;install package
    new (packages)
    if '$data(packages) {
        write !,"Nothing to install"
        quit
    }
    do RegisterAllSourceTypes()
    write !,"Installing packages: "
    for i=1:1:packages {
        #dim package As %String = packages(i)
        #dim packageFile As CPM.Source.IBase = ##class(CPM.Source.IBase).%OpenContainer(package)
        if '$isobject(packageFile) {
            continue
        }
        if packageFile.IsPacked {
            do packageFile.Unpack()
        }
        #dim packageData As CPM.IPackage = packageFile.Parse()
        write !?3, packageData.Name, ?25, packageData.Version, ?45, packageData.Description,!,packageData.Path,!
        do packageFile.Install(packageData)
    }
    quit

SEARCH(string, args...) ;;search packages
    set cpm=##class(CPM.Main).%New()
    do cpm.Search(string)
    quit
    

LIST(args...) ;;list of installed packages
    new (args)
    #dim verbose As %Boolean = 0
    for i=1:1:$get(args) {
        // FIXME - rework as generic getopt() parser
        if $get(args(i)) = "/verbose" {
            set verbose = 1
        }
    }
    set sql=##class(%SQL.Statement).%New()
    set tSC=sql.%Prepare("SELECT %ID, Name, Version, Description FROM CPM_Registry.Package")
    quit:'$$$ISOK(tSC)
    set tRS=sql.%Execute()
    while tRS.%Next() {
        write !?3,tRS.Name,?32,tRS.Version,?42,tRS.Description
        if verbose {
            #dim o As CPM.Registry.Package = ##class(CPM.Registry.Package).%OpenId(tRS.ID)
            #dim items As array of %String = o.Items
            #dim key As %String = ""
            for  {
                set key = items.Next(key) quit:key=""
                write ?10,key,!
            }
        }
    }
    quit

HELP(args...) ;;this help
    new (args)
    write !,"Cache Package Manager"
    write !!,"Available commands:"
    for i=1:1 {
        set line=$text(COMMANDS+i)
        quit:line=""

        continue:$zstrip($extract(line),"*WCP")=""
        set help=$piece(line,";;",2)
        continue:help=""
        set command=$zconvert($piece(line,"("),"L")
        write !?3,command,?15,help
    }
    write !?3,"quit",?15,"quit from shell"
    write !
    quit
    

]]></Routine>
</Export>
