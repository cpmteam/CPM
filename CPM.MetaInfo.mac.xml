<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Routine name="CPM.MetaInfo" type="MAC" languagemode="0"><![CDATA[
#include CPM.License
#include CPM.DataTypes

ProcessManifest(FileName,&pkg,&deps,Debug=0) [xmltree] {
    new xmltree
    do ArrayFromXml(FileName, $name(xmltree))
    merge pkg       = xmltree("Manifest", 1, "Package")
    merge localdeps = xmltree("Manifest", 2, "Dependencies")

    if Debug { write pkg("Name"),":",! }

    // FIXME - simpler for 1:1:deps?
    set i = $order(localdeps(""))
    while i'="" {
        #dim name As %String = localdeps(i,"Dependency","Name")

        set deps(name) = $get(localdeps(i,"Dependency","Version"),
                              $get(localdeps(i,"Dependency","Path"))) ; create exportable list of deps

        if Debug {
            write ?4, name, ?20, deps(name), !
        }
        set i = $order(localdeps(i))
    }
    if Debug { zw xmltree }
}

CreateManifestPath(rootdir,package) public {
    if '$data(package) || (package="") {
        quit ""
    }
    #dim dir As %String = ##class(%Library.File).NormalizeDirectory(package,rootdir)
    quit ##class(%Library.File).NormalizeFilename("manifest.xml",dir)
}

/// dump dependencies, optionally recursive and by request with whole xml tree
DumpDeps(FileName,Recursive=0,Debug=0) [queue,known] public {
    #dim directory As %String = ##class(%Library.File).GetDirectory(FileName)
    #dim rootdir As %String = ##class(%Library.File).ParentDirectoryName(directory)
    
    new queue, known
    $$$INITQUEUE(queue) $$$ENQUEUE(queue,"*")
    
    #dim i As %String
    #dim pkg, deps
    
    do {
        $$$DEQUEUE(queue)
        kill pkg, deps
        do ProcessManifest(FileName, .pkg, .deps, Debug)
        #dim pkgName As %String = pkg("Name")

        set known(pkgName) = 1 ; mark itself as visited
        
        #dim name As %String = $order(deps(""))
        while name '="" {
            if '$data(known(name)) {
                $$$ENQUEUE(queue,name)
                set known(name) = 1
            }
            set name = $order(deps(name))
        }
        set FileName = $$CreateManifestPath(rootdir, $$$QUEUEHEAD(queue))
    } while Recursive && $$$QUEUELEN(queue)
}

Dump(FileName) public {
    // TODO - convert XML to multidimensional array, then use it elsewhere
    #dim XmlParser As %XML.TextReader
    do ##class(%XML.TextReader).ParseFile(FileName,.XmlParser)
    while XmlParser.Read() {
        write XmlParser.NodeType, ?20, XmlParser.Name, ?32, XmlParser.Value,!
        for i=1:1:XmlParser.AttributeCount {
            do XmlParser.MoveToAttributeIndex(i)
            write ?8, XmlParser.NodeType, ?20, XmlParser.Name, ?32, XmlParser.Value,!
        }
    }
}

 /*
    <Manifest>
    <Package Name="CPM-Installer-Sample" Version = "0.5.0" 
         License="MIT" Path="https://github.com/intersystems/monlbl-viewer.git" />
    <Dependencies>
    <Dependency Name="async" Version=">= 0.2.10"/>
    <Dependency Name="language-cos" Path="https://github.com/UGroup/atom-language-cos.git"/>
    </Dependencies>
    </Manifest>

 get loaded as local array
    
    %xmltree("Manifest")=2
    %xmltree("Manifest",1,"Package","License")="MIT"
    %xmltree("Manifest",1,"Package","Name")="CPM-Installer-Sample"
    %xmltree("Manifest",1,"Package","Path")="https://github.com/intersystems/monlbl-viewer.git"
    %xmltree("Manifest",1,"Package","Version")="0.5.0"
    %xmltree("Manifest",2,"Dependencies")=2
    %xmltree("Manifest",2,"Dependencies",1,"Dependency","Name")="async"
    %xmltree("Manifest",2,"Dependencies",1,"Dependency","Version")=">= 0.2.10"
    %xmltree("Manifest",2,"Dependencies",2,"Dependency","Name")="language-cos"
    %xmltree("Manifest",2,"Dependencies",2,"Dependency","Path")="https://github.com/UGroup/atom-language-cos.git"
*/

#; Load metainfo from given XML file into "local name"
#; this name could be local, process private or global name
ArrayFromXml(FileName,LocalName) public {
    #dim xreader As %XML.TextReader
    do ##class(%XML.TextReader).ParseFile(FileName,.xreader)

    #dim modeStack As %ArrayOfDataTypes
    #dim arrayMode As %Boolean
    $$$INITSTACK(modeStack)

    while xreader.Read() {
        // opening tag e.g. <Package>
        if xreader.NodeType = "element" {
            // push tag
            set arrayMode =  $$$TOPSTACK(modeStack)
            if (arrayMode) {
                set LocalName = $name(@LocalName@($increment(@LocalName),xreader.Name))
            } else {
                set LocalName = $name(@LocalName@(xreader.Name))
            }
            // we are switching to "array mode" if there is no
            // single attribute in the current tag 
            set arrayMode = '(xreader.AttributeCount > 0)
            $$$PUSHVALUE(modeStack,arrayMode)
            if arrayMode {
                set @LocalName = 0
            }
        }
        // closing tag e.g. </Package>
        elseif xreader.NodeType = "endelement" {
            // pop last tag
            // FIXME -make sure that the last popped subscript is the same as in xreader.Name
            $$$POPVALUE(modeStack)
            set arrayMode =  $$$TOPSTACK(modeStack)
            set LocalName = $name(@LocalName,$qlength(LocalName) - (arrayMode + 1))
        }
        // xml tag attributes e.g. <Package Name="CPM-Installer-Sample" Version = "0.5.0" />
        for i=1:1:xreader.AttributeCount {
            do xreader.MoveToAttributeIndex(i)
            set @LocalName@(xreader.Name) = xreader.Value
        }
    }
    //zw @LocalName
}
]]></Routine>
</Export>
