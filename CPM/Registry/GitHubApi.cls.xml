<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="CPM.Registry.GitHubApi">
<IncludeCode>CPM.LIB</IncludeCode>
<Super>%RegisteredObject</Super>
<TimeCreated>64004,11169.348775</TimeCreated>

<Parameter name="ApiServer">
<Default>api.github.com</Default>
</Parameter>

<Method name="GetCertificate">
<Description>
Get SSL Configuration by Server as a name for configuration</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>Server:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    new $namespace
    zn "%SYS" 
    quit:##class(Security.SSLConfigs).Exists(Server) Server

    do {
        set tSC=##class(Security.SSLConfigs).Create(Server)
        $$$ThrowOnError(tSC)
    } while 0
    
    quit Server
]]></Implementation>
</Method>

<Method name="SendSimpleHttpRequest">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[server:%String="api.github.com",location:%String,&responseText:%String,authorization:%String=""]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim req as %Net.HttpRequest = ##class(%Net.HttpRequest).%New()
    set req.Server      = server
    set req.Https       = 1
    set req.ContentType = "application/json"
    set req.AcceptGzip  = 1
    set req.Timeout     = $$$GetParam(Params, "timeout", 30)
    set req.SSLConfiguration = ..GetCertificate(server)
    if authorization '= "" {
        set req.Authorization = authorization
    }
    
    #dim status as %Status = $$$OK
    set status = req.Get(location)

    if ($$$ISOK(status)) {
        #dim response as %String = ""
        #dim res as %Net.HttpResponse = req.HttpResponse
            
        #dim responseLength as %Integer = $$$MaxReasonableResponseSize
        set response = res.Data.Read(.responseLength)
        
        set responseText = response
    } else {
        set responseText = ""
    }
        
    quit status
]]></Implementation>
</Method>

<Method name="GetTokenAuthorization">
<ClassMethod>1</ClassMethod>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
    #dim token As %String = $$$CpmConfig."GitHub.Token"
    #dim authorization As %String = ""
    if token '= "" {
        set authorization = "token "_token
    }
    return authorization
]]></Implementation>
</Method>

<Method name="CallApiArgs">
<Description><![CDATA[
Call GitHub API given <var>api</var> urls and arguments. Return JSON data into <var>responseText</var>]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>apiServer:%String,api:%String,arrayArgs:%ArrayOfDataTypes,*responseText:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = ""
    
    if $match(api,"^https?:.*/.*") { // Full URL given in api will override everything
        // do ..SplitUrl(api, .url, .api)
        set url = api
    } else {
        if $match(apiServer, "^https?:.*") {
            set url = apiServer
        } else {
            set url = "https://" _ apiServer
        }
        if $match(api, "^/.*") {
            set url = url _ api
        } else {
            set url = url _ "/" _ api
        }
    }
#if 0
    /// no need to do any of OAuth tricks
    #dim authorizer As TWReader.Authorizer = ##class(TWReader.Authorizer).%New()
    #dim authHeader as %String = authorizer.GetAuthHeader("get", url, arrayArgs)
#else
    #dim authHeader As %String = ..GetTokenAuthorization()
#endif
    set responseText = ""
    quit ..SendSimpleHttpRequest(apiServer, api, .responseText, authHeader)
]]></Implementation>
</Method>

<Method name="GetUrl">
<Internal>1</Internal>
<ClassMethod>1</ClassMethod>
<FormalSpec>url:%String,*object:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim jsonContent As %String = ""
    #dim status As %Status = ..CallApiArgs(..#ApiServer, url, , .jsonContent)
    quit:$$$ISERR(status) status

    set status = ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(jsonContent, ,.object)
    return status
]]></Implementation>
</Method>

<Method name="GetUserInfo">
<ClassMethod>1</ClassMethod>
<FormalSpec>userName:%String,*object:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = "/users/" _ userName
    return ..GetUrl(url, .object)
]]></Implementation>
</Method>

<Method name="GetRepoInfo">
<ClassMethod>1</ClassMethod>
<FormalSpec>repoName:%String,*object:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = "/repos/" _ repoName
    return ..GetUrl(url, .object)
]]></Implementation>
</Method>

<Method name="GetReleases">
<ClassMethod>1</ClassMethod>
<FormalSpec>repoName:%String,*list:%ListOfObjects</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = "/repos/" _ repoName _ "/releases"
    return ..GetUrl(url, .list)
]]></Implementation>
</Method>

<Method name="GetLicense">
<ClassMethod>1</ClassMethod>
<FormalSpec>repoName:%String,*object:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = "/repos/" _ repoName _ "/license"
    return ..GetUrl(url, .object)
]]></Implementation>
</Method>

<Method name="GetReadme">
<ClassMethod>1</ClassMethod>
<FormalSpec>repoName:%String,*object:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = "/repos/" _ repoName _ "/readme"
    return ..GetUrl(url, .object)
]]></Implementation>
</Method>

<Method name="GetReleaseInfo">
<ClassMethod>1</ClassMethod>
<FormalSpec>repoName:%String,releaseName:%String="latest",*object:%ZEN.proxyObject</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim url As %String = "/repos/" _ repoName _ "/releases/" _ releaseName
    return ..GetUrl(url, .object)
]]></Implementation>
</Method>

<Method name="DebugApiCall">
<ClassMethod>1</ClassMethod>
<FormalSpec>apiUrl:%String="/rate_limit",apiParams:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
    #dim array as %ArrayOfDataTypes = ##class(%ArrayOfDataTypes).%New()
    #dim param As %String
    #dim i As %Integer
    
    if ($get(apiParams)="") {
        if $length(apiUrl, "?")>1 {
            set $lb(apiUrl, apiParams) = $lfs(apiUrl, "?")
        }
    }
    if $get(apiParams,"")'="" {
        for i=1:1:$length(apiParams,"&") {
            set param = $piece(apiParams,"&", i)
            #dim arg As %String     = $piece(param,"=",1)
            #dim value As %String   = $piece(param,"=",*)
            do array.SetAt(value, arg)
        }
    }
    #dim jsonContent As %String
    #dim status As %Status = ..CallApiArgs(..#ApiServer, apiUrl, array, .jsonContent)
    quit:$$$ISERR(status) status

    // ok - now we have json in text, but it won't be readable much
    // let's try to parse it to object and then output in readable form
    #dim proxyObject As %ZEN.proxyObject
    set status = ##class(%ZEN.Auxiliary.jsonProvider).%ConvertJSONToObject(jsonContent, ,.proxyObject)
    quit:$$$ISERR(status) status
    
    // write indented (in a readable form) to the screen
    quit ##class(%ZEN.Auxiliary.jsonProvider).%ObjectToJSON(proxyObject)
]]></Implementation>
</Method>
</Class>
</Export>
